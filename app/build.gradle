apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jacoco'

jacoco {
    toolVersion "0.7.6.201602180812"
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
        sourceDirectories = files(android.sourceSets.main.java.srcDirs)
        classDirectories = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
        executionData = fileTree(dir: buildDir, includes: [
                '/jacoco/testDebugUnitTest.exec',
                'outputs/code-coverage/connected/*coverage.ec'
        ])
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.dlgdev.goivcalc"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    kapt compilerDeps.dagger_processor
    kapt compilerDeps.dagger_compiler
    kapt compilerDeps.butterknife_compiler
    kapt compilerDeps.lifecycle_compiler
    kapt compilerDeps.room_compiler

    implementation runtime.butterknife
    implementation runtime.dagger
    implementation runtime.appcompat
    implementation runtime.constraint_layout
    implementation runtime.kotlin_stdlib
    implementation runtime.recycler_view
    implementation runtime.gson
    implementation runtime.lifecycle
    implementation runtime.lifecycle_extensions
    implementation runtime.room
    implementation runtime.retrofit
    implementation runtime.retrofit_gson

    testImplementation testDeps.junit
    testImplementation testDeps.mockito_core

    androidTestImplementation(testDeps.mockito)
    androidTestImplementation(testDeps.espresso_core, {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
}
